(* Diagnostic for the lenet training step's default schedule: prints the fission segment census
   (kinds, launch geometry, per-nest loop extents and written nodes) on the configured backend, then
   times a few individual steps. Clone of bench_conv.ml's model; not part of the benchmark
   protocol. *)

open Base
open Ocannl
module IDX = Train.IDX
open Nn_blocks.DSL_modules
module Asgns = Ir.Assignments
module St = Safetensors
module H = Bench_harness

let cross_entropy_loss = Nn_blocks.cross_entropy_loss

let () =
  let fixture = Stdlib.Sys.getenv "BENCH_FIXTURE" in
  let materialize = H.env_flag "BENCH_MATERIALIZE" in
  let st = St.read fixture in
  let batch_size = H.meta_int st "batch_size" in
  let lr = Float.of_string (H.get_meta st "lr") in
  let c1 = H.meta_int st "channels1" in
  let c2 = H.meta_int st "channels2" in
  let k = H.meta_int st "kernel_size" in
  let fc1_dim = H.meta_int st "fc1" in
  let fc2_dim = H.meta_int st "fc2" in
  let x_nd = St.to_ndarray st "x" in
  let y_nd = St.to_ndarray st "y" in
  let total = (Ir.Ndarray.dims x_nd).(0) in
  let n_batches = total / batch_size in
  let xs = TDSL.rebatch ~l:"xs" x_nd () in
  let ys = TDSL.rebatch ~l:"ys" y_nd () in
  let batch_n, bindings = IDX.get_static_symbol ~static_range:n_batches IDX.empty in
  let%op batch_x = xs @| batch_n in
  let%op batch_y = ys @| batch_n in
  let s1 = H.meta_int_default st "stride1" ~default:1 in
  let s2 = H.meta_int_default st "stride2" ~default:1 in
  let conv1 =
    Nn_blocks.conv2d ~label:[ "conv1" ] ~kernel_size:k ~stride:s1 ~use_padding:false
      ~out_channels:c1 ()
  in
  let conv2 =
    Nn_blocks.conv2d ~label:[ "conv2" ] ~kernel_size:k ~stride:s2 ~use_padding:false
      ~out_channels:c2 ()
  in
  let pool1 = Nn_blocks.max_pool2d ~stride:2 ~window_size:2 () in
  let pool2 = Nn_blocks.max_pool2d ~stride:2 ~window_size:2 () in
  let fc1 = Nn_blocks.mlp_layer ~label:[ "fc1" ] ~hid_dim:fc1_dim () in
  let fc2 = Nn_blocks.mlp_layer ~label:[ "fc2" ] ~hid_dim:fc2_dim () in
  let%op model x =
    ({ w_logits } * fc2 (fc1 (pool2 (relu (conv2 (pool1 (relu (conv1 x)))))))) + { b_logits = 0. }
  in
  let logits = model batch_x in
  let%op batch_loss =
    cross_entropy_loss ~spec:"...|v" ~normalize_by:!..batch_size () ~logits ~targets:batch_y
  in
  if materialize then Train.every_non_literal_materialized batch_loss;
  let step_comp =
    if H.env_flag "BENCH_FWD" then Train.forward batch_loss
    else
      let update = Train.grad_update batch_loss in
      let learning_rate = TDSL.O.( !. ) lr in
      let sgd = Train.sgd_update ~learning_rate batch_loss in
      Asgns.sequence [ update; sgd ]
  in
  let ctx = Context.auto () in
  let backend = Context.backend_name ctx in
  let limits = Context.hardware_limits ctx in
  let ctx = Train.init_params ctx bindings batch_loss in
  let mapping =
    [
      ("conv1_kernel", [ "conv1"; "kernel" ]);
      ("conv1_bias", [ "conv1"; "bias" ]);
      ("conv2_kernel", [ "conv2"; "kernel" ]);
      ("conv2_bias", [ "conv2"; "bias" ]);
      ("fc1_w", [ "fc1"; "w" ]);
      ("fc1_b", [ "fc1"; "b" ]);
      ("fc2_w", [ "fc2"; "w" ]);
      ("fc2_b", [ "fc2"; "b" ]);
      ("w_logits", [ "w"; "logits" ]);
      ("b_logits", [ "b"; "logits" ]);
    ]
  in
  let kc1 () =
    match
      Base.Set.find batch_loss.Tensor.params ~f:(fun p ->
          String.is_substring (Ir.Tnode.debug_name p.Tensor.value) ~substring:"kernel_conv1")
    with
    | Some p -> p.Tensor.value
    | None -> assert false
  in
  let trace = H.env_flag "BENCH_TRACE" in
  let show ctx stage =
    if trace then
      let vs = Context.get_values ctx (kc1 ()) in
      Stdio.printf "kc1 %-14s sum: %.6g first: %.6g\n" stage
        (Array.fold vs ~init:0. ~f:( +. ))
        vs.(0)
  in
  show ctx "after-init";
  let late_inject = H.env_flag "BENCH_LATE_INJECT" in
  let ctx = if late_inject then ctx else H.inject ctx st batch_loss mapping in
  show ctx "after-inject";
  (* First compile only stashes the lowered code for the census (an explicit transform replaces the
     default pipeline, so this routine is discarded); the timed routine is compiled with the regular
     default pipeline below. *)
  let stash = ref None in
  let _census_ctx, _census_routine =
    Context.compile
      ~lowered_transform:(fun opt ->
        stash := Some opt;
        opt)
      ctx step_comp bindings
  in
  let opt = Option.value_exn ~here:[%here] !stash in
  let promote_locals =
    match Stdlib.Sys.getenv_opt "BENCH_PROMOTE" with Some "0" -> Some false | _ -> None
  in
  H.print_census ?promote_locals ~backend ~limits ~static_indices:[ batch_n ] opt;
  (* Which accumulations the gh-ocannl-484 task-3 detector actually proposes on this graph — the
     seeded family can only reach the segments listed here, so this is what says whether the
     dominant segment of the census above is even a candidate. *)
  (if H.env_flag "BENCH_SR_SITES" then
     let sites = Autotune.split_reduce_sites opt in
     Stdio.printf "split-reduce sites detected: %d\n" (List.length sites);
     List.iter sites ~f:(fun s ->
         Stdio.printf "  %s: reduction extent %d, target cells %d%s\n"
           (Ir.Tnode.debug_name s.Autotune.sr_target)
           s.Autotune.sr_red s.Autotune.sr_out
           (if s.Autotune.sr_dynamic then " (scatter)" else "")));
  let t0 = Unix.gettimeofday () in
  let ctx, routine = Context.compile ctx step_comp bindings in
  let compile_s = Unix.gettimeofday () -. t0 in
  let ctx = if late_inject then H.inject ctx st batch_loss mapping else ctx in
  show ctx "after-compile";
  Stdio.printf "backend: %s  compile_s: %.3f\n" backend compile_s;
  (* Per-segment (per-layer) times: populate intermediates with one full step, then time each
     fission segment as its own routine. *)
  (if H.env_flag "BENCH_SEG_TIMES" then (
     let batch_ref = IDX.find_exn (Context.bindings routine) batch_n in
     batch_ref := 0;
     Train.run ctx routine;
     Context.sync ctx;
     H.time_segments ?promote_locals ~backend ~limits ~static_indices:[ batch_n ] ~ctx
       ~comp:step_comp ~bindings
       ~bind:(fun r -> IDX.find_exn (Context.bindings r) batch_n := 0)
       opt));
  (if H.env_flag "BENCH_STEPS" then
     let batch_ref = IDX.find_exn (Context.bindings routine) batch_n in
     for step = 0 to 2 do
       batch_ref := step % n_batches;
       let t0 = Unix.gettimeofday () in
       Train.run ctx routine;
       Context.sync ctx;
       Stdio.printf "step %d: %.1f ms\n" step ((Unix.gettimeofday () -. t0) *. 1000.);
       Stdio.Out_channel.flush Stdio.stdout
     done);
  if H.env_flag "BENCH_PROBE" then (
    let batch_ref = IDX.find_exn (Context.bindings routine) batch_n in
    batch_ref := 0;
    Train.run ctx routine;
    Context.sync ctx;
    let dump name tn n =
      let vs = Context.get_values ctx tn in
      Stdio.printf "%s[0..%d]: %s\n" name (n - 1)
        (String.concat ~sep:" "
           (Array.to_list (Array.map (Array.sub vs ~pos:0 ~len:n) ~f:(Printf.sprintf "%.5g"))));
      Stdio.printf "%s sum: %.6g\n" name (Array.fold vs ~init:0. ~f:( +. ))
    in
    ignore logits;
    dump "loss" batch_loss.Tensor.value 1;
    Hashtbl.iter_keys opt.Ir.Low_level.traced_store ~f:(fun tn ->
        match Context.get_values ctx tn with
        | vs ->
            Stdio.printf "node %s sum: %.6g first: %.6g\n" (Ir.Tnode.debug_name tn)
              (Array.fold vs ~init:0. ~f:( +. ))
              vs.(0);
            if H.env_flag "BENCH_DUMP" then (
              let f =
                Stdio.Out_channel.create
                  (Printf.sprintf "/tmp/probe_%s.txt" (Ir.Tnode.debug_name tn))
              in
              Array.iter vs ~f:(fun v -> Stdio.Out_channel.fprintf f "%.9g\n" v);
              Stdio.Out_channel.close f)
        | exception _ -> ());
    Base.Set.iter batch_loss.Tensor.params ~f:(fun p ->
        let tn = p.Tensor.value in
        let vs = Context.get_values ctx tn in
        Stdio.printf "param %s sum: %.6g first: %.6g\n" (Ir.Tnode.debug_name tn)
          (Array.fold vs ~init:0. ~f:( +. ))
          vs.(0)))
